programa Ej5
procesos
  
  proceso juntarPapel(ES noHayPapel: boolean)
  comenzar
    si(HayPapelEnLaEsquina)
      tomarPapel
    sino
      noHayPapel := V
  fin
  
  proceso dejarPapel
  comenzar
    si(HayPapelEnLaBolsa)
      depositarPapel
  fin
  
  proceso buscarPapel(ES noHayPapel: boolean)
  variables
    avAct, calleAct: numero
  comenzar
    avAct := PosAv
    calleAct := PosCa
    BloquearEsquina(11,11)
    Pos(11,11)
    juntarPapel(noHayPapel)
    Pos(avAct, calleAct)
    LiberarEsquina(11,11)
  fin
  
  proceso actualizarMaximo(E id: numero; E calle: numero; ES idMax: numero; ES calleMax: numero)
  comenzar
    si(calle > calleMax)
      calleMax := calle
      idMax := id
  fin
  
areas
  aR1: AreaP(4,1,4,100)
  aR2: AreaP(6,1,6,100)
  aR3: AreaP(8,1,8,100)
  aR4: AreaP(10,1,10,100)
  aC: AreaP(1,1,1,1)
  esq11y11: AreaPC(11,11,11,11) 

robots

  robot robot1
  variables
    noHayPapel: boolean
    id, calleFinal: numero
  comenzar
    RecibirMensaje(id, RCoord)
    mientras((PosCa < 100) & (noHayPapel = F))
      buscarPapel(noHayPapel)
      si(noHayPapel = F) {si la variable nHP es F significa que juntó papel. Avanza y vuelve al while.}
        dejarPapel
        mover
    {en este punto ya terminó de moverse, sea porque no hay papeles o porque completó la av.}
    calleFinal := PosCa {si no hago esto tira error}
    EnviarMensaje(id, RCoord)
    EnviarMensaje(calleFinal, RCoord)    
  fin
  
  robot robotCoord
  variables
    id, idMax, calle, calleMax: numero
  comenzar
    calleMax := -1
    EnviarMensaje(1, R1)
    EnviarMensaje(2, R2)
    EnviarMensaje(3, R3)
    EnviarMensaje(4, R4)
    
    repetir 4
      RecibirMensaje(id, *)
      si(id = 1)
        RecibirMensaje(calle, R1)
      sino
        si(id = 2)
          RecibirMensaje(calle, R2)
        sino
          si(id = 3)
            RecibirMensaje(calle, R3)
          sino
            RecibirMensaje(calle, R4)
    actualizarMaximo(id, calle, idMax, calleMax)
    
    Informar(idMax)
  fin
  
variables
  R1: robot1
  R2: robot1
  R3: robot1
  R4: robot1
  RCoord: robotCoord
  
comenzar
  AsignarArea(R1, aR1)
  AsignarArea(R1, esq11y11)
  
  AsignarArea(R2, aR2)
  AsignarArea(R2, esq11y11)
  
  AsignarArea(R3, aR3)
  AsignarArea(R3, esq11y11)
  
  AsignarArea(R4, aR4)
  AsignarArea(R4, esq11y11)
  
  AsignarArea(RCoord, aC)
  
  Iniciar(RCoord, 1, 1)
  Iniciar(R1, 4, 1)
  Iniciar(R2, 6, 1)
  Iniciar(R3, 8, 1)
  Iniciar(R4, 10, 1)
fin