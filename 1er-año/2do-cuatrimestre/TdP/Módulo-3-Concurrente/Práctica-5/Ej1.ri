programa Ej5
procesos
  proceso juntarFlores(ES cantFlores: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      cantFlores := cantFlores + 1
  fin

  proceso juntarPapeles(ES cantPapeles: numero)
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      cantPapeles := cantPapeles + 1
  fin

  proceso recorrerCalle(ES cantPapeles: numero; E N: numero)
  comenzar
    repetir N
      juntarPapeles(cantPapeles)
      mover
    juntarPapeles(cantPapeles)
  fin

  proceso recorrerAv(ES cantFlores: numero; E N: numero)
  comenzar
    repetir N
      juntarFlores(cantFlores)
      mover
    juntarFlores(cantFlores)
  fin

areas
  aPC: AreaPC(1, 98, 3, 100)
  aR1: AreaP(2, 1, 2, 97)
  aR2: AreaP(3, 1, 3, 97)
  aA1: AreaP(4, 98, 100, 98)
  aA2: AreaP(4, 99, 100, 99)
  aF: AreaP(1, 1, 1, 1)
robots
  robot eqRojo
  variables
    av, calle, cantFlores: numero
  comenzar
    cantFlores := 0
    av := PosAv
    recorrerAv(cantFlores, 96)
    BloquearEsquina(av, 98)
    mover
    juntarFlores(cantFlores)
    BloquearEsquina(av, 99)
    mover
    LiberarEsquina(av, 98)
    juntarFlores(cantFlores)
    mover
    LiberarEsquina(av, 99)
    juntarFlores(cantFlores)
    EnviarMensaje(cantFlores, RF)
  fin

  robot eqAzul
  variables
    av, calle, cantPapeles: numero
  comenzar
    cantPapeles := 0
    calle := PosCa
    derecha
    juntarPapeles(cantPapeles)
    BloquearEsquina(2, calle)
    mover
    juntarPapeles(cantPapeles)
    BloquearEsquina(3, calle)
    mover
    LiberarEsquina(2, calle)
    juntarPapeles(cantPapeles)
    mover
    LiberarEsquina(3, calle)
    recorrerCalle(cantPapeles, 96)
    EnviarMensaje(cantPapeles, RF)
  fin

  robot robotFisc
  variables
    flores, floresTotal, papeles, papelesTotal: numero
  comenzar
    RecibirMensaje(flores, R1)
    RecibirMensaje(floresTotal, R2)
    RecibirMensaje(papeles, A1)
    RecibirMensaje(papelesTotal, A2)
    floresTotal := floresTotal + flores
    papelesTotal := papelesTotal + papeles
    si(floresTotal > papelesTotal)
      Informar(1) {1: ganó el equipo rojo.}
    sino
      si(papelesTotal > floresTotal)
        Informar(2) {2: ganó el equipo azul.}
      sino
        Informar(0) {0: empate.}
  fin

variables
  R1: eqRojo
  R2: eqRojo
  A1: eqAzul
  A2: eqAzul
  RF: robotFisc
comenzar
  AsignarArea(R1, aR1)
  AsignarArea(R1, aPC)

  AsignarArea(R2, aR2)
  AsignarArea(R2, aPC)

  AsignarArea(A1, aA1)
  AsignarArea(A1, aPC)

  AsignarArea(A2, aA2)
  AsignarArea(A2, aPC)

  AsignarArea(RF, aF)

  Iniciar(R1, 2, 1)
  Iniciar(R2, 3, 1)
  Iniciar(A1, 1, 98)
  Iniciar(A2, 1, 99)
  Iniciar(RF, 1, 1)
fin